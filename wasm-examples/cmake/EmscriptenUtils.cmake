macro(EMSCRIPTEN_ENSURE_TSC TSC_VERSION)
    if(NOT DEFINED TSC_VERSION)
        set(TSC_VERSION "5.9.3")
    endif()

    get_filename_component(EMSCRIPTEN_ROOT "${CMAKE_CXX_COMPILER}" DIRECTORY)
    get_filename_component(EMSCRIPTEN_ROOT "${EMSCRIPTEN_ROOT}" REALPATH)
    set(BUNDLED_EMSCRIPTEN_ROOT "${CMAKE_CURRENT_SOURCE_DIR}/emsdk/upstream/emscripten")
    if (EXISTS "${BUNDLED_EMSCRIPTEN_ROOT}")
        get_filename_component(BUNDLED_EMSCRIPTEN_ROOT "${BUNDLED_EMSCRIPTEN_ROOT}" REALPATH)
    else ()
        unset(BUNDLED_EMSCRIPTEN_ROOT)
    endif ()
    set(TSC_BIN "${EMSCRIPTEN_ROOT}/node_modules/.bin/tsc")
    if (NOT EXISTS "${TSC_BIN}")
        set(TSC_INSTALL_HINT
                "TypeScript is required to emit native TypeScript declarations. Run `cd ${EMSCRIPTEN_ROOT} && npm install --no-save --package-lock false typescript@${TSC_VERSION}` then reconfigure.")
        if (DEFINED BUNDLED_EMSCRIPTEN_ROOT AND EMSCRIPTEN_ROOT STREQUAL BUNDLED_EMSCRIPTEN_ROOT)
            find_program(NPM_EXECUTABLE npm)
            if (NOT NPM_EXECUTABLE)
                message(FATAL_ERROR "npm was not found but is required to install TypeScript. ${TSC_INSTALL_HINT}")
            endif ()
            execute_process(
                    COMMAND ${NPM_EXECUTABLE} install --no-save --package-lock false typescript@${TSC_VERSION}
                    WORKING_DIRECTORY "${EMSCRIPTEN_ROOT}"
                    RESULT_VARIABLE NPM_TSC_RESULT
            )
            if (NPM_TSC_RESULT)
                message(FATAL_ERROR "npm failed to install TypeScript (exit code ${NPM_TSC_RESULT}). ${TSC_INSTALL_HINT}")
            endif ()
            if (NOT EXISTS "${TSC_BIN}")
                message(FATAL_ERROR "TypeScript installation finished but tsc is still missing. ${TSC_INSTALL_HINT}")
            else ()
                message(STATUS "Installed TypeScript ${TSC_VERSION} for the bundled Emscripten SDK.")
            endif ()
        else ()
            message(FATAL_ERROR "${TSC_INSTALL_HINT}")
        endif ()
    endif ()
endmacro()